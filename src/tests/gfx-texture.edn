(def timestep (/ 1.0 120.0))
(defmesh Root)
(def BlendAlpha {:Operation BlendOperation.Add :Src BlendFactor.SrcAlpha :Dst BlendFactor.OneMinusSrcAlpha})
(defloop test-wire
  (Setup
   0.0 >= .time
   (GFX.BuiltinMesh :Type BuiltinMeshType.Cube) >= .mesh
   
   ; Load texture
   (LoadImage "../../assets/ShardsLogo.png") >= .image
   (Log "Image")
   (GFX.Texture) >= .texture
   (Log "Texture")

   (Float3 0 0 0) (Math.Translation) >= .transform
   {:Transform .transform :Mesh .mesh :Textures {:baseColor .texture}} (GFX.Drawable :Transform .transform) >= .drawable

    ; Create render steps
   (GFX.BuiltinFeature :Id BuiltinFeatureId.Transform) >> .features
   (GFX.BuiltinFeature :Id BuiltinFeatureId.BaseColor) >> .features
   {:Features .features} (GFX.DrawablePass) >> .render-steps

    ; Create view
   {:Position (Float3 0 0 8) :Target (Float3 0 0 0)} (Math.LookAt) >= .view-transform
   (GFX.View :View .view-transform) >= .view)
  (GFX.MainWindow
   :Title "SDL Window" :Width 1280 :Height 720 :Debug false
   :Contents
   (->
    .time (Math.Add timestep) > .time
    .time (Math.Multiply 0.2) (Math.AxisAngleX) (Math.Rotation) >= .rotX
    .time (Math.Multiply 0.7) (Math.AxisAngleY) (Math.Rotation) >= .rotY
    .time (Math.Multiply 0.9) (Math.AxisAngleZ) (Math.Rotation) >= .rotZ
    .rotX (Math.MatMul .rotY) (Math.MatMul .rotZ) > .transform
    .drawable (GFX.Draw)
    (GFX.Render :Steps .render-steps :View .view))))

(schedule Root test-wire)
(if (run Root timestep 100) nil (throw "Root tick failed"))
